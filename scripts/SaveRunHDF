#!/bin/zsh -f

# Configuration --------------------------------------------------------------

MYDIR=${$(readlink -f $0):h}
BG_CONDITION=${MYDIR}/bg_condition.txt
SIG_CONDITION=${MYDIR}/sig_condition.txt
# allow override if already set in the environment
: ${OUT_HDF:=/work/mdean/h5files}
: ${OUT_TAGS:=/home/mdean/data/config}

# ----------------------------------------------------------------------------

zmodload zsh/zutil
zparseopts -E -D -- \
    -qsub=opt_qsub \
    n=opt_dryrun -dry-run=opt_dryrun -debug=opt_debug \
    h=opt_help -help=opt_help

if [[ $# == 0 || -n ${opt_help} ]]; then
    print "usage: ${0:t} [options] run-number"
    print
    print "Options:"
    print "  -n, --dry-run    display job script and exit"
    print "  -h, --help   display this message and exit"
    print "  --qsub       use qsub to run this from a front-end node"
    print "  --debug      save files to PWD instead of standard paths"
    exit
fi

JOBNAME="${0:t}-$1"

# check output directories exist
ls -d "${OUT_HDF?}" "${OUT_TAGS?}" > /dev/null || exit $?


if [[ -n $opt_debug ]]; then
    OUT_HDF=$PWD
    OUT_TAGS=$PWD
fi

alias LaunchJob='/bin/bash'
if [[ -n ${opt_dryrun} ]]; then
    alias LaunchJob='cat'
elif [[ -n ${opt_qsub} ]]; then
    alias LaunchJob='qsub -m n -N ${JOBNAME}'
fi

TAGS_BG=${OUT_TAGS}/tags_${1}_bg.lst
TAGS_SIG=${OUT_TAGS}/tags_${1}_sig.lst

optmtl_common=( -b 3 -r $1 )
optmtl_bg=( $optmtl_common -out $TAGS_BG -inp $BG_CONDITION )
optmtl_sig=( $optmtl_common -out $TAGS_SIG -inp $SIG_CONDITION )

optdc_common=( -dir $OUT_HDF -f ${MYDIR}/RunData_wMotors.conf )
optdc_bg=( $optdc_common -l $TAGS_BG -o run_${1}_bg.h5 )
optdc_sig=( $optdc_common -l $TAGS_SIG -o run_${1}_sig.h5 )

LaunchJob <<< "\
cd $PWD || exit 2
MakeTagList $optmtl_bg &&
DataConvert4 $optdc_bg

MakeTagList $optmtl_sig &&
DataConvert4 $optdc_sig
"
